; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include AddBackslash(CompilerPath) + "ISPPBuiltins.iss"

#define MainFileHead "BatteryMode"
#define MainWindowClass "TBatteryModeForm"
#define AppMutex "BatteryModMutex"
#define AppPublisherURL "https://bmode.tarcode.ru"
#define AppSupportURL "https://bmode.tarcode.ru"
#define AppUpdatesURL "https://bmode.tarcode.ru/releasenotes"
#define SetupPostfix "Installer"

#define MainFileName MainFileHead + BitsInstall + ".exe"
#define Build "..\Build\"
#define MainFile AddBackslash(SourcePath) + Build + MainFileName

#define AppName GetFileDescription(MainFile)  
#define AppVersion GetFileVersion(MainFile)
#define FileCompany GetFileCompany(MainFile)
#define FileCopyright GetFileCopyright(MainFile) 
#define Description GetFileDescription(MainFile)
#define ProductName GetStringFileInfo(MainFile, PRODUCT_NAME)
#define ProductVersion GetFileProductVersion(MainFile)

#define InstallFolder AppName

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={#ProductName}
AppName={#ProductName}
AppVersion={#AppVersion}
AppVerName={#AppName} {#AppVersion}
AppCopyright={#FileCopyright}
AppPublisher={#FileCompany}
AppPublisherURL={#AppPublisherURL}
AppSupportURL={#AppSupportURL}
AppUpdatesURL={#AppUpdatesURL}
DisableWelcomePage=no
DefaultDirName={localappdata}\{#InstallFolder}
DefaultGroupName={#AppName}
AllowNoIcons=yes
OutputDir={#Build}
OutputBaseFilename={#MainFileHead}{#SetupPostfix}{#BitsInstall}
SetupIconFile="..\Icon\icon.ico"
Compression=lzma
SolidCompression=yes
PrivilegesRequired=asis
ArchitecturesInstallIn64BitMode={#if BitsInstall == '64'}x64{#endif}
MinVersion=0,5.01
UninstallDisplayName={#ProductName}
UninstallDisplayIcon={app}\{#MainFileName}
AppMutex={code:GetAppMutex}
DisableDirPage=no
DirExistsWarning=auto
UsePreviousAppDir=yes
Uninstallable=IsUninstallable()
VersionInfoVersion={#AppVersion}
VersionInfoCopyright={#FileCopyright}
VersionInfoDescription={#Description} {#SetupPostfix}
VersionInfoProductName={#ProductName}
VersionInfoProductVersion={#ProductVersion}
WizardImageFile=Image\WizardImage.bmp
WizardSmallImageFile=Image\WizardImageSmall.bmp

#include "Localization.iss"
#include "Framework/Versions.Helpers.iss"
#include "Framework/Process.Kill.iss"
#include "Framework/Setup.InstallType.Gui.iss"

[Messages]
HelpTextNote=%nThe Setup program accepts advanced command line parameters.%n/PORTABLE%n/INSTALLTYPE=AllUser|JustForMe|Portable%n

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; Check: not IsPortableOnly
Name: "AutoRun"; Description: "{cm:AutoStartProgram,{#AppName}}"; GroupDescription: "{cm:AutoStartProgramGroupDescription}"; Check: not IsPortableOnly

[Files]
Source: "{#Build}{#MainFileName}"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#ProductName}"; Filename: "{app}\{#MainFileName}"; IconFilename: "{app}\{#MainFileName}"; IconIndex: 0
Name: "{commondesktop}\{#ProductName}"; Filename: "{app}\{#MainFileName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MainFileName}"; Description: "{cm:LaunchProgram,{#ProductName}}"; Flags: nowait postinstall skipifsilent; Check: not IsPortableOnly

[Registry]
;current user only
Root: "HKCU"; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "{#AppName}"; ValueData: "{app}\{#MainFileName}"; Flags: uninsdeletevalue; Tasks: AutoRun; Check: not IsElevated; BeforeInstall: DelAutorun
;any user
Root: "HKLM"; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "{#AppName}"; ValueData: "{app}\{#MainFileName}"; Flags: uninsdeletevalue; Tasks: AutoRun; Check: IsElevated; BeforeInstall: DelAutorun

[Code]

var
	WpSysIcons: TWizardPage;
	SysIconButton: TNewButton;

function OpenSystemIcon: Boolean;
var
	ResultCode: Integer;
begin
	if IsWindowsVersionOrGreater(10, 0, 14328) then
		Result := Exec('rundll32', 'shell32.dll Options_RunDLL 1', '', SW_SHOW, ewNoWait, ResultCode)
	else if IsWindowsVistaOrGreater then
		Result := Exec('rundll32', 'shell32.dll Options_RunDLL 4', '', SW_SHOW, ewNoWait, ResultCode)
	else
		Result := Exec('rundll32', 'shell32.dll Options_RunDLL 1', '', SW_SHOW, ewNoWait, ResultCode);
end;

procedure DelAutorun();
var
	ResultCode: Integer;
begin
	Exec(ExpandConstant('{app}\{#MainFileName}'), '-DelAutorun', '', SW_HIDE, ewWaitUntilTerminated, ResultCode)
end;

function GetAppMutex(Param: String): string;
begin
	if IsWindowsVistaOrGreater then
		Result := ''
	else
		Result := ExpandConstant('{#AppMutex}');
end;

procedure SysIconButtonClick(Sender: TObject);
begin
	OpenSystemIcon();
end;

procedure OnCurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
	if CurUninstallStep = usUninstall then
	begin
		SendCloseMessageByClassName('{#MainWindowClass}');
	end;
end;

procedure OnInitializeWizard();
var
	SysIconsDescLabel: TNewStaticText;
begin
	WpSysIcons := CreateCustomPage(wpInfoAfter, CustomMessage('SysIcons'), CustomMessage('SysIconsDescription'));
	
	SysIconsDescLabel := TNewStaticText.Create(WizardForm);
	with SysIconsDescLabel do begin
		Parent := WpSysIcons.Surface;
		AutoSize := False;
		Top := ScaleY(8);
		Width := WpSysIcons.SurfaceWidth - Left*2;
		Wordwrap := True;
	end;
  if IsWindows10OrGreater then
  	SysIconsDescLabel.Caption := CustomMessage('SysIconsDescLabelWin10')
  else 
  	SysIconsDescLabel.Caption := CustomMessage('SysIconsDescLabel');
  SysIconsDescLabel.AdjustHeight;
	
	SysIconButton := TNewButton.Create(WizardForm);
	with SysIconButton do begin
		Parent := WpSysIcons.Surface;
		Enabled := not IsPortableOnly;
		Top := SysIconsDescLabel.Top + SysIconsDescLabel.Height + ScaleY(6);
		Height := ScaleY(25);
		Width := ScaleX(230);
		Caption := CustomMessage('SysIconButton');
		OnClick := @SysIconButtonClick;
	end;
end;

function InitializeSetup(): Boolean;
begin
	if ParamExists('/h') then
	begin
		MsgBox(TrimLeft(SetupMessage(msgHelpTextNote)) + #13#10 +
			'For more detailed information, please restart with /HELP parameter', mbInformation, MB_OK);
		Result := False;
		Exit;
	end;
	InstallTypeGuiInit();
	ConnectOnInitializeWizard(@OnInitializeWizard);  
	Result := True;
end;

function InitializeUninstall(): Boolean;
begin
	ConnectOnCurUninstallStepChanged(@OnCurUninstallStepChanged);
	Result := True;
end;
